plugins {
    id 'java-library'
    id 'com.diffplug.spotless' version '7.2.1'
    id "com.vanniktech.maven.publish" version "0.34.0"
    id 'jacoco'
}

group = 'io.github.open-spaced-repetition'
version = '0.7.0'
description = 'Java library for FSRS Spaced Repetition'
java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    compileOnly libs.org.projectlombok.lombok
    annotationProcessor libs.org.projectlombok.lombok

    testImplementation libs.org.assertj.assertj.core
    testImplementation libs.org.junit.jupiter.junit.jupiter

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.19.2'

}

test {
    useJUnitPlatform()
}

spotless {
    format('misc') {
        target '.gitattributes', '.gitignore'
        trimTrailingWhitespace()
        endWithNewline()
        indentWithTabs()
    }
    java {
        googleJavaFormat('1.17.0').aosp().reflowLongStrings().formatJavadoc(false)
        licenseHeader '/* (C)$YEAR */'
    }
}

jacoco {
    toolVersion "0.8.13"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }

    classDirectories.setFrom(
            fileTree(dir: "$buildDir/classes/java/main", excludes: ['**/generated/**'])
    )
    sourceDirectories.setFrom(files('src/main/java'))
    executionData.setFrom(fileTree(buildDir).include("/jacoco/test.exec"))
}


mavenPublishing {
    coordinates(group.toString(), project.name, version.toString())

    pom {
        name = "fsrs"
        description = "Java library for FSRS Spaced Repetition"
        inceptionYear = "2025"
        url = "https://github.com/open-spaced-repetition/java-fsrs"
        licenses {
            license {
                name = "MIT License"
                url = "https://www.opensource.org/licenses/mit-license.php"
                distribution = "https://www.opensource.org/licenses/mit-license.php"
            }
        }
        developers {
            developer {
                id = "joshdavham"
                name = "Joshua Hamilton"
                url = "https://github.com/joshdavham"
            }
        }
        scm {
            url = "https://github.com/open-spaced-repetition/java-fsrs"
            connection = "scm:git:git://github.com/open-spaced-repetition/java-fsrs.git"
            developerConnection = "scm:git:ssh://git@github.com/open-spaced-repetition/java-fsrs.git"
        }
    }
}
